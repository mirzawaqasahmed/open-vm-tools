################################################################################
### Copyright 2007 VMware, Inc.  All rights reserved.
###
### Configure script for building the VMware OSS Tools.
###
###
### This program is free software; you can redistribute it and/or modify
### it under the terms of version 2 of the GNU General Public License as
### published by the Free Software Foundation.
###
### This program is distributed in the hope that it will be useful,
### but WITHOUT ANY WARRANTY; without even the implied warranty of
### MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
### GNU General Public License for more details.
###
### You should have received a copy of the GNU General Public License
### along with this program; if not, write to the Free Software
### Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
################################################################################

################################################################################
# In addition to the usual environment variables and command line
# arguments that a configure script usually takes (CFLAGS, LDFLAGS,
# etc.), this script also accepts:
# $CUSTOM_FUSE_CPPFLAGS - compile-time flags to find FUSE headers.
# $CUSTOM_FUSE_LIBS - link-time flags to find FUSE libraries.
# $CUSTOM_GLIB2_CPPFLAGS - compile-time flags to find Glib headers.
# $CUSTOM_GLIB2_LIBS - link-time flags to find Glib libraries.
# $CUSTOM_GTK_CPPFLAGS - compile-time flags to find Gtk headers.
# $CUSTOM_GTK_LIBS - link-time flags to find and link the Gtk libraries.
# $CUSTOM_PROCPS_LIBS - link-time flags to find and link the procps library.
# $CUSTOM_URIPARSER_CPPFLAGS - compile-time flags to find the uriparser headers
# $CUSTOM_URIPARSER_LIBS - link-time flags to find and link the uriparser library
# $CUSTOM_ZLIB_CPPFLAGS - compile-time flags to find the zlib headers
# $CUSTOM_ZLIB_LIBS - link-time flags to find and link the zlib library
################################################################################

###
### Initialization
###

AC_INIT(
   [open-vm-tools],
   [2009.01.21-142982],
   [open-vm-tools-devel@lists.sourceforge.net])

# In order to make this configure script auto-detect situations where
# people have a 32-bit userland running with a 64-bit kernel, we try to ask
# the compiler (assumedly gcc) for its default Target:.
# We have to set up $TEST_CC manually, since AC_PROG_CC hasn't yet been run (and can't be until AC_CANONICAL_HOST & AC_CANONICAL_BUILD are run)
# The purpose of all this is to set up $host_alias/$build_alias in a more
# intelligent way than config.guess currently does.
TEST_CC="$CC_FOR_BUILD"
test -z "$TEST_CC" && TEST_CC="$HOST_CC"
test -z "$TEST_CC" && TEST_CC="$CC"
if test -n "$TEST_CC" -a -z "$host_alias"; then
   host_alias="`$TEST_CC -dumpmachine`"
   if test -z "$build_alias" -a -n "$host_alias"; then
      build_alias="$host_alias"
   fi
fi
unset TEST_CC

# checkvm/checkvm.c has no special significance - we just need to pass in a file that
# helps autoconf verify that it really has found the source tree.
AC_CONFIG_SRCDIR([checkvm/checkvm.c])

# Keep the top-level directory tidy by putting auxiliary build tools and local
# macros in separate subdirectories.
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_MACRO_DIR([m4])

AC_CANONICAL_HOST
AC_CANONICAL_BUILD

# Quote the regular expressions
case "$host_cpu" in
   [i[3456]86])
      userSpaceBitness="32"
      ;;
   [x86_64])
      userSpaceBitness="64"
      ;;
   [*])
      AC_MSG_ERROR([Unknown architecture.])
      ;;
esac

# Operational arguments.
AC_ARG_WITH([root-privileges],
	    [AS_HELP_STRING([--without-root-privileges],
	       [does not perform any operations that require root privileges])],
	    [],
	    [with_root_privileges=yes])

# Kernel arguments.
# The kernel args have to go here otherwise the KERNEL_RELEASE won't be visible
# to getOsVersion()
AC_ARG_WITH([kernel-modules],
	    [AS_HELP_STRING([--without-kernel-modules],
		[does not compile or install the kernel modules])],
	    [],
	    [with_kernel_modules=yes])

AC_ARG_WITH([kernel-release],
	    [AS_HELP_STRING([--with-kernel-release],
		[specifies the kernel release you want to build against])],
	    [KERNEL_RELEASE="$withval"],
	    [KERNEL_RELEASE=`uname -r`])

AC_ARG_WITH([linuxdir],
	    [AS_HELP_STRING([--with-linuxdir],
		[specifies the Linux directory you want to use])],
	    [LINUXDIR="$withval"],
	    [LINUXDIR=/lib/modules/$KERNEL_RELEASE])

# Turn the uname output into something we can run comparisons on.
getOsVersion() {
   major_version="`echo $KERNEL_RELEASE | cut -f1 -d. | cut -f1 -d-`"
   minor_version="`echo $KERNEL_RELEASE | cut -f2 -d. | cut -f1 -d-`"
   micro_version="`echo $KERNEL_RELEASE | cut -f3 -d. | cut -f1 -d-`"
   printf '%02d%02d%03d' $major_version $minor_version $micro_version
}

case "$host_os" in
   [linux*])
      os="linux"
      ;;
   [freebsd*])
      os="freebsd"
      ;;
   [solaris*])
      os="solaris"
      ;;
   [*])
      AC_MSG_WARN([This is an untested and unsupported Operating System. Proceed at your own peril.])
      ;;
esac
osVersion="`getOsVersion`"
if test "$os" = "linux"; then
   if test "$with_kernel_modules" = "yes"; then
      if test ! -d "$LINUXDIR/kernel/"; then
      	 AC_MSG_ERROR([$LINUXDIR/kernel does not exist])
      fi
   fi
fi

# Arguments for disabling individual open-vm-tools features or libraries.
AC_ARG_ENABLE(
   multimon,
   AS_HELP_STRING(
      [--disable-multimon],
      [disables multimon, enabled by default]),
   [enable_multimon="$enableval"],
   [enable_multimon="yes"])

AC_ARG_ENABLE(
   unity,
   AS_HELP_STRING(
      [--disable-unity],
      [disables Unity, enabled by default]),
   [enable_unity="$enableval"],
   [enable_unity="yes"])

AC_ARG_WITH(
   gtk2,
   AS_HELP_STRING(
      [--without-gtk2],
      [compiles without Gtk 2.0 (falls back on Gtk 1.2)]),
   [with_gtk2="$withval"],
   [with_gtk2="yes"])

# If we're not going to use Gtk 2.0, disable Unity. It may be possible to
# get Unity working with Gtk 1.2 (it used to in the past), but that will
# require a great deal of work.
if test "$with_gtk2" != "yes"; then
   enable_unity="no"
fi

AM_INIT_AUTOMAKE

###
### Constants
###
# These need to be declared after initialization.

# Some of our macro call-sites require changes to
# CPPFLAGS/CFLAGS/LDFLAGS. In such places, we save the original value
# of CPPFLAGS/CFLAGS/LDFLAGS before the macro call and restore it when
# the call is done. We must perform this save at each macro site,
# because CPPFLAGS/CFLAGS/LDFLAGS may change over the course of
# configuration.
#
# CPPFLAGS is intended for preprocessor options (-D and -I mainly)
# CFLAGS is intended for compiler options (-O, -f, -W, and so forth)

CPPFLAGS="$CPPFLAGS -DUSING_AUTOCONF=1"

###
### Programs
###
# C preprocessor and compiler.
AC_PROG_CPP
AC_PROG_CC

# C++ compiler. Note that unlike AC_PROG_CC, this call does not trigger an
# error if no C++ compiler was found; it'll just set the variable CXX to 'g++'.
AC_PROG_CXX

# This allows features like per-target compiler flags.  I.e., you can compile
# one copy of the same sources twice with different flags.  (See lib/guestApp
# for an example.)
AM_PROG_CC_C_O

# Needed for the various install and uninstall hooks.
AC_PROG_INSTALL
AC_PROG_SED
AC_PROG_LN_S
AC_PROG_MKDIR_P

# Needed for creating the archives in lib/ and the shared libraries.
AC_PROG_LIBTOOL
if test "$ac_cv_prog_AR" = false; then
   AC_MSG_ERROR([The 'ar' utility was not found. Please put ar on the path.])
fi

# We use pkg-config to set up the cflags and libs for gtk.
AC_CHECK_PROG(
   [HAVE_PKG_CONFIG],
   [pkg-config],
   [yes],
   [no])

if test "$GCC" != "yes"; then
   AC_MSG_ERROR([Only GCC is currently supported. Please put gcc in the path.])
fi

###
### Libraries
###

AC_PATH_XTRA

#
# Check for glib 2.6.0 or greater.
# XXX: glib < 2.6 should become an error once the code that needs it is the default.
#
if test "$HAVE_PKG_CONFIG" = "yes"; then
   AC_MSG_CHECKING([for glib >= 2.6.0 (via pkg-config)])
   if pkg-config --exists 'glib-2.0 >= 2.6.0'; then
      GLIB2_CPPFLAGS="`pkg-config --cflags glib-2.0`"
      GLIB2_LIBS="`pkg-config --libs glib-2.0`"
      have_glib_26="yes"
      AC_MSG_RESULT([yes])
  else
      have_glib_26="no"
      AC_MSG_RESULT([no])
  fi
else
   # g_key_file_new (and related) functions were added in glib 2.6. That's the
   # minimum version supported by the Core Services code.
   AC_CHECK_LIB(
      [glib-2.0],
      [g_key_file_new],
      [have_glib_26="yes"],
      [AC_MSG_WARN([glib 2 is older than 2.6.0, some features will be disabled.])],
      [$CUSTOM_GLIB2_LIBS])
   if test "$have_glib_26" = "yes"; then
      GLIB2_CPPFLAGS="$CUSTOM_GLIB2_CPPFLAGS"
      GLIB2_LIBS="$CUSTOM_GLIB2_LIBS"
   fi
fi

#
# Check for fuse.
#
if test "$HAVE_PKG_CONFIG" = "yes"; then
   AC_MSG_CHECKING([for fuse (via pkg-config)])
   if pkg-config --exists 'fuse'; then
      FUSE_CPPFLAGS="`pkg-config --cflags fuse`"
      FUSE_LIBS="`pkg-config --libs fuse`"
      have_fuse="yes"
      AC_MSG_RESULT([yes])
  else
      have_fuse="no"
      AC_MSG_RESULT([no])
  fi
else
   ORIGINAL_LDFLAGS="$LDFLAGS"
   LDFLAGS="$CUSTOM_FUSE_LIBS $LDFLAGS"
   AC_CHECK_LIB(
      [fuse],
      [fuse_main],
      [have_fuse="yes";
       if test -n "$CUSTOM_FUSE_LIBS"; then
          FUSE_LIBS="$CUSTOM_FUSE_LIBS"
       else
          FUSE_LIBS="-lfuse"
       fi],
      [have_fuse="no"; AC_MSG_WARN([Fuse is missing, vmblock-fuse will be disabled.])])
   LDFLAGS="$ORIGINAL_LDFLAGS"

   if test "$have_fuse" = "yes"; then
      ORIGINAL_CPPFLAGS="$CPPFLAGS"
      CPPFLAGS="$CUSTOM_FUSE_CPPFLAGS $CPPFLAGS"
      AC_CHECK_HEADER(
         [fuse.h],
         [if test -n "$CUSTOM_FUSE_CPPFLAGS"; then
             FUSE_CPPFLAGS="$CUSTOM_FUSE_CPPFLAGS"
	  else
	     FUSE_CPPFLAGS="-I/usr/include"
          fi],
         [have_fuse="no"; AC_MSG_WARN([Can't locate fuse headers, vmblock-fuse will be disabled.])])
      CPPFLAGS="$ORIGINAL_CPPFLAGS"
   fi
fi

# If the user explicitly disables X11, then don't try to detect the X-related libraries
if test "$have_x" = "disabled"; then
   enable_multimon="no"
   enable_unity="no"
elif test "$have_x" != "yes"; then
   AC_MSG_ERROR(
      [The X11 libraries were not found. Please configure without X11 (using --without-x), or install the libX11 devel package(s).])
else
   CPPFLAGS="$CPPFLAGS $X_CFLAGS"
   COMMON_XLIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"

   AC_CHECK_LIB(
      [Xext],
      [XeviQueryVersion],
      [COMMON_XLIBS="-lXext $COMMON_XLIBS"],
      [AC_MSG_ERROR(
         [libXext not found. Please configure without X11 (using --without-x), or install the libXext devel package(s).])],
      [$COMMON_XLIBS])

   AC_CHECK_HEADER(
      [X11/extensions/extutil.h],
      [],
      [AC_MSG_ERROR(
         [X11/extensions/extutil.h header not found - you're probably on Solaris 10 or older. Please copy that header file onto your system manually, or configure without X11 (using --without-x).])],
      [#include <X11/Xlib.h>
       #include <X11/Xproto.h>])

   if test "$enable_multimon" != "no"; then
      AC_CHECK_LIB(
	 [Xinerama],
	 [XineramaQueryVersion],
	 [COMMON_XLIBS="-lXinerama $COMMON_XLIBS"],
	 [AC_MSG_ERROR(
	    [libXinerama not found. Please configure without multimon (using --disable-multimon), configure without X11 (using --without-x), or install the libXinerama devel package(s).])],
	 [$COMMON_XLIBS])
   else
      if test "$enable_unity" != "no"; then
         AC_MSG_WARN([Disabling Unity due to --disable-multimon.])
         enable_unity="no"
      fi
   fi

   AC_CHECK_LIB(
      [Xrender],
      [XRenderQueryVersion],
      [COMMON_XLIBS="-lXrender $COMMON_XLIBS"],
      [AC_MSG_ERROR(
         [libXrender not found. Please configure without X11 (using --without-x), or install the libXrender devel package(s).])],
      [$COMMON_XLIBS])

   AC_CHECK_LIB(
      [Xrandr],
      [XRRQueryVersion],
      [COMMON_XLIBS="-lXrandr $COMMON_XLIBS"],
      [AC_MSG_ERROR(
	 [libXrandr not found. Please configure without X11 (using --without-x) or install the libXrandr devel package(s).])],
      [$COMMON_XLIBS])

   AC_CHECK_LIB(
      [Xtst],
      [XTestQueryExtension],
      [COMMON_XLIBS="-lXtst $COMMON_XLIBS"],
      [AC_MSG_ERROR(
	 [libXtst not found. Please configure without X11 (using --without-x) or install the libXtst devel package(s).])],
      [$COMMON_XLIBS])

   # If we're building with support for Unity, we'll need a few additional
   # libraries.
   if test "$enable_unity" != "no"; then

           # Unity needs the X11 Screen Saver extension library. It should be
	   # in the same place as the X11 libraries, so no need for any fancy
	   # path checking.
           AC_CHECK_LIB(
              [Xss],
              [XScreenSaverQueryExtension],
              [COMMON_XLIBS="-lXss $COMMON_XLIBS"],
              [AC_MSG_ERROR(
	         [libXss not found. Please configure without Unity (using --disable-unity) or install the libXss devel package.])],
              [$COMMON_XLIBS])

           # Check for the uriparser library and headers. The upstream uriparser
           # doesn't appear to ship with either a pkg-config file or a
           # uriparser-config binary, so we'll look for it the old-fashioned
           # way.
           #
           # Note that we look for uriFreeQueryListA because it's a relatively
           # new symbol that our code needs (it isn't present in the uriparser
           # that shipped with Ubuntu Hardy).
           ORIGINAL_LDFLAGS="$LDFLAGS"
           LDFLAGS="$CUSTOM_URIPARSER_LIBS $LDFLAGS"
           AC_CHECK_LIB(
              [uriparser],
              [uriFreeQueryListA],
              [if test -n "$CUSTOM_URIPARSER_LIBS"; then
                       URIPARSER_LIBS="$CUSTOM_URIPARSER_LIBS"
               else
                       URIPARSER_LIBS="-luriparser"
               fi],
              [AC_MSG_ERROR(
                 [uriparser library not found or is too old. Please configure without Unity (using --disable-unity) or install the liburiparser devel package.])])
           LDFLAGS="$ORIGINAL_LDFLAGS"

           if test -n "$CUSTOM_URIPARSER_CPPFLAGS"; then
                   URIPARSER_CPPFLAGS="$CUSTOM_URIPARSER_CPPFLAGS"
           else
                   # XXX: No pkg-config and no uriparser-config means we need to
	           # hard-code a sensible default.
	           URIPARSER_CPPFLAGS="-I/usr/include/uriparser"
           fi
           ORIGINAL_CPPFLAGS="$CPPFLAGS"
           CPPFLAGS="$URIPARSER_CPPFLAGS $CPPFLAGS"
           AC_CHECK_HEADER(
              [Uri.h],
              [],
              [AC_MSG_ERROR(
                 [uriparser headers not found. Please configure without Unity (using --disable-unity) or install the liburiparser devel package])])
           CPPFLAGS="$ORIGINAL_CPPFLAGS"

           # Check for libpng using pkg-config, or barring that, using
           # libpng-config.
           if test "$HAVE_PKG_CONFIG" = "yes"; then
              AC_MSG_CHECKING([for libpng (via pkg-config)])
              if pkg-config --exists libpng12; then
                      LIBPNG_CPPFLAGS="`pkg-config --cflags libpng12`"
	              LIBPNG_LIBS="`pkg-config --libs libpng12`"
                      AC_MSG_RESULT([yes])
              else
                      AC_MSG_RESULT([no])
              fi
           else
              AC_PATH_PROG(
                 [LIBPNG_CONFIG],
                 [libpng-config],
                 [no])
              if test "$LIBPNG_CONFIG" != "no"; then
                      LIBPNG_CPPFLAGS="`$LIBPNG_CONFIG --cflags`"
                      LIBPNG_LIBS="`$LIBPNG_CONFIG --libs`"
              fi
           fi
           if test -z "$LIBPNG_CPPFLAGS"; then
              AC_MSG_ERROR(
                 [libpng not found. Please configure without Unity (using --disable-unity) or install the libpng devel package])
           fi

           # Check for the zlib library and headers. Like uriparser, there
           # isn't a pkg-config or zlib-config file.
           ORIGINAL_LDFLAGS="$LDFLAGS"
           LDFLAGS="$CUSTOM_ZLIB_LIBS $LDFLAGS"
           AC_CHECK_LIB(
              [z],
              [zlibVersion],
              [if test -n "$CUSTOM_ZLIB_LIBS"; then
                       ZLIB_LIBS="$CUSTOM_ZLIB_LIBS"
               else
                       ZLIB_LIBS="-lz"
               fi],
              [AC_MSG_ERROR(
                 [zlib library not found. Please configure without Unity (using --disable-unity) or install the zlib devel package.])])
           LDFLAGS="$ORIGINAL_LDFLAGS"

           if test -n "$CUSTOM_ZLIB_CPPFLAGS"; then
                   ZLIB_CPPFLAGS="$CUSTOM_ZLIB_CPPFLAGS"
           else
                   # XXX: No pkg-config and no zlib-config means we need to
	           # hard-code a sensible default.
	           ZLIB_CPPFLAGS="-I/usr/include"
           fi
           ORIGINAL_CPPFLAGS="$CPPFLAGS"
           CPPFLAGS="$ZLIB_CPPFLAGS $CPPFLAGS"
           AC_CHECK_HEADER(
              [zlib.h],
              [],
              [AC_MSG_ERROR(
                 [zlib headers not found. Please configure without Unity (using --disable-unity) or install the zlib devel package])])
           CPPFLAGS="$ORIGINAL_CPPFLAGS"
   fi # End of library checks for Unity.

   if test -n "$CUSTOM_GTK_CPPFLAGS" -o -n "$CUSTOM_GTK_LIBS"; then
   # Check to see if we have some version of GTK. gtk_accel_group_add
   # is a random function that is present in Gtk 1.2 but not Gtk
   # 2.0. Similarly, gtk_tree_view_new is a random function in Gtk 2.0
   # which is not present in Gtk 1.2.
      AC_CHECK_LIB(
	 [gtk],
	 [gtk_accel_group_add],
	 [HAVE_GTK1="yes"],,
	 [$CUSTOM_GTK_LIBS])
      if test "$with_gtk2" != "no"; then
         AC_CHECK_LIB(
	    [gtk-x11-2.0],
	    [gtk_tree_view_new],
	    [HAVE_GTK2="yes"],,
	    [$CUSTOM_GTK_LIBS])
      fi
   fi

   # Sometimes pkg-config can find stuff that AC_CHECK_LIB can't, so we use that as well.
   if test "$HAVE_PKG_CONFIG" = "yes"; then
      if test "$with_gtk2" != "no"; then
         AC_MSG_CHECKING([for Gtk+ 2.0 (via pkg-config)])
         if pkg-config --exists gtk+-2.0; then
	    HAVE_GTK2="yes"
            AC_MSG_RESULT([yes])
         else
            AC_MSG_RESULT([no])
         fi
     fi

      AC_MSG_CHECKING([for Gtk+ 1.x (via pkg-config)])
      # The latter half of the test is because pkg-config's fallback
      # to gtk-config is broken on systems such as Ubuntu that use
      # bash-completion for unknown commands. (On Ubuntu, typing
      # 'gtk-config' on a system that doesn't have libgtk1.2-dev
      # installed outputs hints about using apt-get, in addition to the
      # regular bash "command not found" message.)
      if pkg-config --exists gtk+ \
	 && test -n "`pkg-config --libs gtk+ 2>/dev/null | grep -v '^ *$'`"; then
	 HAVE_GTK1="yes"
         AC_MSG_RESULT([yes])
      else
         AC_MSG_RESULT([no])
      fi
   fi

   if test "$HAVE_GTK1" = "yes" -o "$HAVE_GTK2" = "yes"; then
      # This bit is to fix builds on OpenSolaris B71, where
      # 'pkg-config --cflags gtk+-2.0' gives an error.
      if test "$HAVE_GTK2" = "yes" -a "$HAVE_PKG_CONFIG" = "yes" \
	 && ! pkg-config --exists gtk+-2.0 \
	 && test -z "$CUSTOM_GTK_CPPFLAGS" -a -z "$CUSTOM_GTK_LIBS"; then
         HAVE_GTK2="no"
      fi

      if test "$HAVE_GTK2" = "yes"; then
         # Gtk 2.0
	 GTK_CPPFLAGS="$GTK_CPPFLAGS -DGTK2"
	 gtkPackageName="gtk+-2.0"
      elif test "$HAVE_GTK1" = "yes"; then
         # Gtk 1.2
	 gtkPackageName="gtk+"

         # Unity requires Gtk 2.0.
         if test "$enable_unity" != "no"; then
            AC_MSG_ERROR([The Unity feature requires Gtk+ 2.0. Please install the Gtk+ 2.0 devel packages or configure without Unity (using --disable-unity)])
         fi
      else
      # We found Gtk+ 2.0 that turned out to have a non-working
      # pkg-config setup, AND Gtk 1.2 wasn't installed.
	 AC_MSG_ERROR([The Gtk+ library installed on your system is not fully functional. Please contact your OS vendor.])
      fi

      # Prefer flags specified, in the environment, by the user.
      if test -n "$CUSTOM_GTK_CPPFLAGS" -o -n "$CUSTOM_GTK_LIBS"; then
	 GTK_CPPFLAGS="$GTK_CPPFLAGS $CUSTOM_GTK_CPPFLAGS"
	 GTK_LIBS="$GTK_LIBS $CUSTOM_GTK_LIBS"
      # Use pkg-config if available and there are no user-specified flags.
      elif test "$HAVE_PKG_CONFIG" = "yes"; then
	 GTK_CPPFLAGS="$GTK_CPPFLAGS $(pkg-config --cflags $gtkPackageName)"
	 GTK_LIBS="$GTK_LIBS $(pkg-config --libs $gtkPackageName)"
      # Otherwise, we have a problem.
      else
	 AC_MSG_ERROR([pkg-config not found. Get pkg-config or set CUSTOM_GTK_CPPFLAGS and CUSTOM_GTK_LIBS to specify gtk header file and library locations.])
      fi

      # Check for a sufficiently new installation of Gtk 2.0. This symbol is, at
      # the time of writing, used in vmware-user. We must perform this check
      # after GTK_LIBS has been set up. Note: we don't actually use the
      # resultant HAVE_NEW_GTK2 variable; it's just set so that the default
      # action, which will append gdk-x11-2.0 to LIBS, isn't taken.
      if test "$HAVE_GTK2" == "yes"; then
         AC_CHECK_LIB(
            [gdk-x11-2.0],
            [gdk_display_get_default_group],
            [HAVE_NEW_GTK2="yes"],
            [AC_MSG_ERROR(
               [Gtk 2.0 installation may be too old. Please upgrade your Gtk 2.0 packages or configure without Gtk 2.0 (using --without-gtk2)])]
            [$GTK_LIBS])
      fi

      # Unity requires libgdk_pixbuf_xlib, which most systems don't seem to
      # provide via "pkg-config --libs gtk+-2.0".
      if test "$enable_unity" != "no"; then
         AC_CHECK_LIB(
            [gdk_pixbuf_xlib-2.0],
            [gdk_pixbuf_xlib_init],
            [GTK_LIBS="$GTK_LIBS -lgdk_pixbuf_xlib-2.0"],
            [AC_MSG_ERROR(
               [gdk_pixbuf_xlib-2.0 not found. Make sure your Gtk 2.0 installation is sane, or configure without Unity (using --disable-unity).])],
            [$GTK_LIBS])
      fi
   else
      AC_MSG_ERROR([libgtk not found. Please install the gtk devel package(s).])
   fi
fi # End of checks for X libraries

AC_CHECK_LIB(
   [crypt],
   [crypt],
   [HAVE_CRYPT="yes"
    GUESTD_LDADD="$GUESTD_LDADD -lcrypt"],
   [AC_MSG_ERROR(
      [libcrypt not found. Please install the libc/libcrypt devel package(s).])])

AC_CHECK_LIB(
   [notify],
   [notify_init],
   [HAVE_NOTIFY="yes"
    VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lnotify"],
   [AC_MSG_WARN([libnotify not found; notification disabled])])

AC_CHECK_FUNCS(
   dlopen,
   ,
   [AC_CHECK_LIB(
      dl,
      dlopen,
      GUESTD_LDADD="$GUESTD_LDADD -ldl",
      [AC_MSG_ERROR(
         [dlopen was not found, but is required for open-vm-tools to function properly. Please contact your OS vendor.])])])

AC_CHECK_FUNCS([ecvt])
AC_CHECK_FUNCS([fcvt])

AC_CHECK_LIB(
   [pthread],
   [pthread_mutex_init],
   [GUESTD_LDADD="$GUESTD_LDADD -lpthread"
    VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lpthread"],
   [AC_MSG_ERROR(
      [libpthread not found. Please install the libc/libpthread devel package(s).])])

# PAM prefix
AC_ARG_WITH([pam-prefix],
	    [AS_HELP_STRING([--with-pam-prefix],
		[specifies where pam files go. Default is $(sysconfdir)])],
	    [PAM_PREFIX="$withval"],
	    [PAM_PREFIX='$(sysconfdir)'])

if test "$os" = "linux"; then
        AC_ARG_WITH([procps],
	    [AS_HELP_STRING([--without-procps],
		[compiles without libproc (disables support for meminfo)])],
	    [],
	    [with_procps=yes])
else
	with_procps="no"
fi

if test "$with_procps" = "yes"; then
	ORIGINAL_LDFLAGS="$LDFLAGS"
	LDFLAGS="$CUSTOM_PROCPS_LIBS $LDFLAGS"
	AC_CHECK_LIB(
	   [proc-3.2.7],
	   [getstat],
	   [if test -n "$CUSTOM_PROCPS_LIBS"; then
		    PROCPS_LIBS="$CUSTOM_PROCPS_LIBS"
	    else
		    PROCPS_LIBS="-lproc-3.2.7"
	    fi],
	   [AC_MSG_ERROR(
	      [libproc not found. Please configure without procps (using --without-procps) or install procps - http://procps.sourceforge.net])])
	LDFLAGS="$ORIGINAL_LDFLAGS"

	GUESTD_LDADD="$GUESTD_LDADD $PROCPS_LIBS"
	VMWARE_USER_LDADD="$VMWARE_USER_LDADD $PROCPS_LIBS"
fi

if test "$with_procps" != "yes"; then
AC_DEFINE([NO_PROCPS], 1, [Define to 1 if building without procps.])
fi

AC_ARG_WITH([dnet],
	    [AS_HELP_STRING([--without-dnet],
	    [compiles without libdnet (disables support for nicinfo)])],
	    [],
	    [with_dnet=yes])

if test "$with_dnet" = "yes"; then

	# On Debian, dnet is installed via the libdumbnet package. We need to
	# detect this so that our source files include dumbnet.h instead of
	# dnet.h, which is part of a different package altogether.
	AC_CHECK_PROGS([DNET_CONFIG],
		       [dumbnet-config dnet-config],
		       [no])

	if test "$DNET_CONFIG" != no; then
		DNET_CPPFLAGS="`$DNET_CONFIG --cflags`"
		DNET_LIBS="`$DNET_CONFIG --libs`"

		GUESTD_LDADD="$GUESTD_LDADD $DNET_LIBS"
		VMWARE_USER_LDADD="$VMWARE_USER_LDADD $DNET_LIBS"
		if test "$DNET_CONFIG" == dumbnet-config; then
		AC_DEFINE([DNET_IS_DUMBNET], 1, [Define to 1 if substituting Debian's libdumbnet for libdnet.])
		fi
	else
		AC_MSG_ERROR(
		   [dnet-config was not found on your PATH. Please configure without dnet (using --without-dnet) or install dnet - http://libdnet.sourceforge.net])
	fi
fi

if test "$with_dnet" != "yes"; then
AC_DEFINE([NO_DNET], 1, [Define to 1 if building without libdnet.])
fi

AC_ARG_WITH([icu],
            [AS_HELP_STRING([--without-icu],
              [disables support for ICU])],
            [],
            [with_icu=yes])

if test "$with_icu" = "yes"; then

	AC_PATH_PROG([ICU_CONFIG],
		     [icu-config],
		     [no])

	if test "$ICU_CONFIG" != no; then
		ICU_CPPFLAGS="`$ICU_CONFIG --cppflags` -DUSE_ICU"
		ICU_LIBS="`$ICU_CONFIG --ldflags`"

		# The icu components are built with C++, so we need to ensure
		# that the C++ linker actually exists, and use it when linking
		# icu into our applications and when performing configuration
		# tests against icu.
		#
		# XXX: Getting automake to choose between the C linker and the
		# C++ linker depending on HAVE_ICU was a royal pain in the ass.
		# The classic way to do this is to define an optional source
		# file for a program with an extension of .cxx, using
		# nodist_EXTRA_fooprogram_SOURCES. This causes automake's
		# linker detection algorithm to see a C++ source file and
		# automatically set up the C++ linker and link line for us.
		# Unfortunately, said linker detection doesn't obey
		# conditionals, which means that it'd always pick the C++
		# linker, regardless of the value of HAVE_ICU. Instead, we are
		# forced to manually set the correct linker in fooprogram_LINK.
		# However, since none of our programs actually contain C++
		# code, automake doesn't make the CXXLINK variable (which
		# contains the linker as well as all link flags) available to
		# us, so we must hard-code the entire link line into
		# fooprogram_LINK. Not exactly a futureproof solution...
		#
		# Additional references on this problem:
		# http://sources.redhat.com/ml/automake/1999-10/msg00101.html
		# http://lists.gnu.org/archive/html/bug-automake/2008-04/msg00010.html
		# http://www.gnu.org/software/automake/manual/automake.html#Libtool-Convenience-Libraries
		# http://www.gnu.org/software/automake/manual/automake.html#C_002b_002b-Support
		AC_CHECK_TOOL(
		   [HAVE_CXX],
		   [$CXX])
		if test -z "$HAVE_CXX"; then   
		        AC_MSG_ERROR(
			   [No C++ compiler was found on your PATH. Please configure without ICU (using --without-icu) or install a C++ compiler.])
		fi
		AC_LANG_PUSH([C++])
		AC_CHECK_LIB([icuuc],
			     [ucasemap_utf8ToTitle_3_8],
			     [have_ICU_38="yes"],,
			     [$ICU_LIBS])
		AC_LANG_POP([C++])

		if test "$have_ICU_38" = "yes"; then
			ICU_CPPFLAGS="$ICU_CPPFLAGS -DHAVE_ICU_38"
		fi

		# Easier to give all modules the ICU defines/includes...
		CPPFLAGS="$CPPFLAGS $ICU_CPPFLAGS"
	else
		AC_MSG_ERROR(
		   [icu-config was not found on your PATH. Please configure without ICU (using --without-icu) or install ICU - http://www.icu-project.org])
	fi
fi

AC_PATH_PROG(
   [RPCGEN],
   [rpcgen],
   [ AC_MSG_ERROR([rpcgen not found. Please install the libc devel package.]) ])

###
### Headers
###

AC_CHECK_HEADERS([crypt.h])
AC_CHECK_HEADERS([inttypes.h])
AC_CHECK_HEADERS([stdint.h])
AC_CHECK_HEADERS([stdlib.h])
AC_CHECK_HEADERS([wchar.h])
AC_CHECK_HEADERS([sys/inttypes.h])
AC_CHECK_HEADERS([sys/io.h])
AC_CHECK_HEADERS([sys/param.h]) # Required to make the sys/user.h check work correctly on FreeBSD
AC_CHECK_HEADERS([sys/sysinfo.h])
AC_CHECK_HEADERS([sys/types.h])
AC_CHECK_HEADERS([sys/user.h],
   [],
   [],
   [
#ifdef HAVE_SYS_PARAM_H
#   include <sys/param.h>
#endif
   ])
AC_CHECK_HEADERS([sys/vfs.h])
AC_CHECK_HEADERS([syslimits.h])
AC_CHECK_HEADERS([unwind.h])

AC_CHECK_HEADER(
   [wchar.h],
   [HAVE_WCHAR_H="yes"],
   [HAVE_WCHAR_H="no"])

if test "$enable_multimon" != "no"; then
   AC_CHECK_HEADER(
      [X11/extensions/panoramiXproto.h],
      [],
      [AC_MSG_ERROR(
         [panoramiXproto.h not found. Please configure without multimon (using --disable-multimon) or install the libXinerama devel package(s).])],
      [#include <X11/X.h>
       #include <X11/Xmd.h>])
fi

###
### Typdefs, structs, and compiler quarks.
###
AC_HEADER_STDBOOL
AC_C_CONST
AC_TYPE_UID_T
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_CHECK_MEMBERS([struct stat.st_rdev])
AC_HEADER_TIME
AC_STRUCT_TM
AC_C_VOLATILE

###
### Specific features and OS/arch flags / actions
###

# Check to see if SYS_setuid32 is defined. If SYS_setuid32 is unknown, we will
# certainly error out.
if test "$os$userSpaceBitness" = "linux32"; then
   AC_MSG_CHECKING([for SYS_setuid32])
   AC_TRY_COMPILE(
      [#include <sys/syscall.h>
       #include <unistd.h>],
      [
         syscall(SYS_setuid32, 1);
      ],
      [HAVE_SYS_SETUID="yes"],
      [HAVE_SYS_SETUID="no"])
   AC_MSG_RESULT($HAVE_SYS_SETUID)
fi

# Newer iconv (from glibc 2.2 and later on Linux) takes a char ** as the second
# argument. Here, we test for the older form and expect a compiler warning/error
# if the newer form is defined (because, in that case, we would be attempting to
# remove const'ness without an explicit cast).
AC_MSG_CHECKING([for the new format of iconv])
ORIGINAL_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS -Werror"
AC_TRY_COMPILE(
   [#include <iconv.h>],
   [
      iconv_t cd;
      char const *inbuf;
      size_t inbytesleft;
      char *outbuf;
      size_t outbytesleft;

      iconv(cd, &inbuf, &inbytesleft, &outbuf, &outbytesleft);
   ],
   [HAVE_NEW_ICONV="no"],
   [HAVE_NEW_ICONV="yes"])
CFLAGS="$ORIGINAL_CFLAGS"
AC_MSG_RESULT($HAVE_NEW_ICONV)

if test "$os" = "linux"; then
   AC_CHECK_FUNCS(
      [lseek],
      [HAVE_LSEEK="yes"],
      [HAVE_LSEEK="no"])

   # setresuid() was wrapped beginning in glibc 2.3.2.
   AC_MSG_CHECKING([for setresuid])
   # Ensure an error is thrown if setresuid is unknown.
   ORIGINAL_CFLAGS="$CFLAGS"
   CFLAGS="$CFLAGS -Werror-implicit-function-declaration -Werror"
   AC_TRY_COMPILE(
      [#define _GNU_SOURCE
       #include <unistd.h>],
      [
         setresuid(0, 0, 0);
      ],
      [HAVE_SETRESUID="yes"],
      [HAVE_SETRESUID="no"])
   CFLAGS="$ORIGINAL_CFLAGS"
   AC_MSG_RESULT($HAVE_SETRESUID)
fi

### General flags / actions
CFLAGS="$CFLAGS -Wall"
CFLAGS="$CFLAGS -Werror"

# -Wno-unknown-pragmas is due to gcc not understanding '#pragma ident' in Xlib.h on OpenSolaris.
for TEST_CFLAG in -Wno-pointer-sign -Wno-unused-value -fno-strict-aliasing \
    -Wno-unknown-pragmas -Wno-uninitialized; do
    AC_MSG_CHECKING([for GCC flag $TEST_CFLAG])
    ORIGINAL_CFLAGS="$CFLAGS"
    CFLAGS="$CFLAGS $TEST_CFLAG"
    NEW_CFLAG=""
    AC_TRY_COMPILE(
       [],
       [
	return 0;
       ],
   [NEW_CFLAG=" $TEST_CFLAG"
    AC_MSG_RESULT(yes)],
   [AC_MSG_RESULT(no)])
   CFLAGS="$ORIGINAL_CFLAGS$NEW_CFLAG"
done
CPPFLAGS="$CPPFLAGS -DVMX86_TOOLS"
CPPFLAGS="$CPPFLAGS -DNO_CORE_ICU"

BUILDDIR="`pwd`"

INCLUDE_DIR="`cd $srcdir; pwd`/lib/include"
BLD_INCLUDE_DIR="$BUILDDIR/lib/include"
CPPFLAGS="$CPPFLAGS -I$INCLUDE_DIR -I$BLD_INCLUDE_DIR"

### OS/arch-specific flags / actions

MODULES=""
MODULES_OS="$os"
TARGET_OS="$os"
MODULES_DIR=""
buildHgfsmounter=no

if test "$os" = "linux"; then
   MODULES_DIR="$LINUXDIR/kernel/"

   CPPFLAGS="$CPPFLAGS -D_FILE_OFFSET_BITS=64"
   CPPFLAGS="$CPPFLAGS -D_LARGEFILE64_SOURCE"
   CPPFLAGS="$CPPFLAGS -D_XOPEN_SOURCE=500"
   CPPFLAGS="$CPPFLAGS -D_BSD_SOURCE"
   CPPFLAGS="$CPPFLAGS -D_SVID_SOURCE"

   if test "$osVersion" -ge 202000; then
      MODULES="$MODULES vmmemctl vmxnet"
   fi
   if test "$osVersion" -ge 204000; then
      MODULES="$MODULES vmblock vmci vmhgfs vsock"
      buildHgfsmounter=yes
   fi
   if test "$osVersion" -ge 206000; then
      MODULES="$MODULES vmxnet3"
   fi
   if test "$osVersion" -ge 206006; then
      MODULES="$MODULES vmsync"
   fi
   if test "$osVersion" -ge 206008; then
      MODULES="$MODULES pvscsi"
   fi
fi

if test "$os" = "freebsd"; then
   MODULES_DIR="/boot/modules"

   if test "$osVersion" -ge 302000; then
      MODULES="$MODULES vmmemctl"
   fi
   if test "$osVersion" -ge 409000; then
      MODULES="$MODULES vmxnet"
   fi
   if test "$osVersion" -ge 600000; then
      MODULES="$MODULES vmblock vmhgfs"
      buildHgfsmounter=yes
   fi
fi

if test "$os" = "solaris"; then
   LIB_IMPERSONATE_CPPFLAGS="$LIB_IMPERSONATE_CPPFLAGS -D_POSIX_PTHREAD_SEMANTICS"
   LIB_USER_CPPFLAGS="$LIB_USER_CPPFLAGS -D_POSIX_PTHREAD_SEMANTICS"

   GUESTD_LDADD="$GUESTD_LDADD -lsocket"
   GUESTD_LDADD="$GUESTD_LDADD -lnsl"
   GUESTD_LDADD="$GUESTD_LDADD -lrpcsvc"

   VMWARE_USER_CPPFLAGS="$VMWARE_USER_CPPFLAGS -I/usr/X11/include/"
   VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lXext"
   VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lX11"
   VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lsocket"
   VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lnsl"
   VMWARE_USER_LDADD="$VMWARE_USER_LDADD -lrpcsvc"

   # uname -r on Solaris 10 reports "5.10"
   if test "$osVersion" -ge 510000; then
      CPPFLAGS="$CPPFLAGS -DSOL10"
      MODULES="$MODULES vmhgfs"
   fi
fi

AM_CONDITIONAL(BUILD_HGFSMOUNTER, test "$buildHgfsmounter" = "yes")
AM_CONDITIONAL(LINUX, test "$os" = "linux")
AM_CONDITIONAL(SOLARIS, test "$os" = "solaris")
AM_CONDITIONAL(FREEBSD, test "$os" = "freebsd")
AM_CONDITIONAL(THIRTY_TWO_BIT_USERSPACE, test "$userSpaceBitness" = "32")
AM_CONDITIONAL(HAVE_X11, test "$have_x" = "yes")
AM_CONDITIONAL(HAVE_ICU, test "$with_icu" = "yes")
AM_CONDITIONAL(WITH_KERNEL_MODULES, test "$with_kernel_modules" = "yes")
AM_CONDITIONAL(PAMD, test -d /etc/pam.d)
AM_CONDITIONAL(ENABLE_UNITY, test "$enable_unity" = "yes")
AM_CONDITIONAL(WITH_ROOT_PRIVILEGES, test "$with_root_privileges" = "yes")
AM_CONDITIONAL(ENABLE_CORE_SERVICES, test "$have_glib_26" = "yes")
AM_CONDITIONAL(HAVE_FUSE, test "$have_fuse" = "yes")
AM_CONDITIONAL(HAVE_GNU_LD, test "$with_gnu_ld" = "yes")

### Feature-specific flags / actions
# Combine where possible

# If control reaches this point and multimon is still enabled, then we know
# all of the tests for required components have passed and it's safe to allow
# multimon. Otherwise, it should be disabled.
if test "$enable_multimon" = "no"; then
   # XXX: For consistency, change this to ENABLE_MULTIMON. This will require
   # some additional code cleanup.
   AC_DEFINE([NO_MULTIMON], 1, [Define to 1 if building without multimon support.])
fi

if test "$os$userSpaceBitness" = "linux64" || test "$HAVE_SYS_SETUID" = "yes"; then
   LIB_MISC_CPPFLAGS="$LIB_MISC_CPPFLAGS -DGLIBC_VERSION_21"
fi

if test "$HAVE_NEW_ICONV" = "yes"; then
   LIB_MISC_CPPFLAGS="$LIB_MISC_CPPFLAGS -DGLIBC_VERSION_22"
fi

if test "$HAVE_LSEEK" = "yes"; then
   LIB_FILE_CPPFLAGS="$LIB_FILE_CPPFLAGS -DGLIBC_VERSION_21"
   LIB_HGFS_SERVER_CPPFLAGS="$LIB_HGFS_SERVER_CPPFLAGS -DGLIBC_VERSION_21"
fi

if test "$HAVE_SETRESUID" = "yes"; then
   LIB_PROC_MGR_CPPFLAGS="$LIB_PROC_MGR_CPPFLAGS -DGLIBC_VERSION_23"
fi

if test "$HAVE_CRYPT" = "yes"; then
   LIB_AUTH_CPPFLAGS="$LIB_AUTH_CPPFLAGS -DGLIBC_VERSION_22"
fi

if test "$HAVE_WCHAR_H" = "yes"; then
   LIB_STRING_CPPFLAGS="$LIB_STRING_CPPFLAGS -DGLIBC_VERSION_22"
fi

### Core Services definitions.

VMTOOLS_LIBS="$BUILDDIR/libvmtools/libvmtools.la"
VMTOOLS_CPPFLAGS="-DVMTOOLS_USE_GLIB $GLIB2_CPPFLAGS"

### General substs

AC_SUBST([TARGET_OS])
AC_SUBST([KERNEL_RELEASE])
AC_SUBST([MODULES_OS])
AC_SUBST([MODULES_DIR])
AC_SUBST([MODULES])
AC_SUBST([COMMON_XLIBS])
AC_SUBST([GLIB2_CPPFLAGS])
AC_SUBST([GLIB2_LIBS])
AC_SUBST([GTK_CPPFLAGS])
AC_SUBST([GTK_LIBS])
AC_SUBST([DNET_CPPFLAGS])
AC_SUBST([DNET_LIBS])
AC_SUBST([PROCPS_LIBS])
AC_SUBST([ICU_CPPFLAGS])
AC_SUBST([ICU_LIBS])
AC_SUBST([PAM_PREFIX])
AC_SUBST([URIPARSER_CPPFLAGS])
AC_SUBST([URIPARSER_LIBS])
AC_SUBST([LIBPNG_CPPFLAGS])
AC_SUBST([LIBPNG_LIBS])
AC_SUBST([VMTOOLS_CPPFLAGS])
AC_SUBST([VMTOOLS_LIBS])
AC_SUBST([ZLIB_CPPFLAGS])
AC_SUBST([ZLIB_LIBS])
AC_SUBST([FUSE_CPPFLAGS])
AC_SUBST([FUSE_LIBS])

### Lib substs

AC_SUBST([LIB_AUTH_CPPFLAGS])
AC_SUBST([LIB_FILE_CPPFLAGS])
AC_SUBST([LIB_HGFS_SERVER_CPPFLAGS])
AC_SUBST([LIB_IMPERSONATE_CPPFLAGS])
AC_SUBST([LIB_MISC_CPPFLAGS])
AC_SUBST([LIB_PROC_MGR_CPPFLAGS])
AC_SUBST([LIB_STRING_CPPFLAGS])
AC_SUBST([LIB_USER_CPPFLAGS])

LIBVMTOOLS_LIBADD="$GUESTD_LDADD"
AC_SUBST([LIBVMTOOLS_LIBADD])

### Program substs

AC_SUBST([GUESTD_LDADD])

AC_SUBST([VMWARE_USER_CPPFLAGS])
AC_SUBST([VMWARE_USER_LDADD])

###
### Create the Makefiles
###
AC_CONFIG_FILES([                      \
   Makefile                            \
   lib/Makefile                        \
   lib/appUtil/Makefile                \
   lib/auth/Makefile                   \
   lib/backdoor/Makefile               \
   lib/conf/Makefile                   \
   lib/dict/Makefile                   \
   lib/dnd/Makefile                    \
   lib/dynxdr/Makefile                 \
   lib/err/Makefile                    \
   lib/eventManager/Makefile           \
   lib/file/Makefile                   \
   lib/foundryMsg/Makefile             \
   lib/ghIntegration/Makefile          \
   lib/ghIntegrationStub/Makefile      \
   lib/guestApp/Makefile               \
   lib/guestInfo/Makefile              \
   lib/guestRpc/Makefile               \
   lib/hgfs/Makefile                   \
   lib/hgfsBd/Makefile                 \
   lib/hgfsServer/Makefile             \
   lib/hgfsServerManagerGuest/Makefile \
   lib/hgfsServerPolicyGuest/Makefile  \
   lib/deployPkg/Makefile              \
   lib/image/Makefile                  \
   lib/impersonate/Makefile            \
   lib/message/Makefile                \
   lib/misc/Makefile                   \
   lib/netUtil/Makefile                \
   lib/panic/Makefile                  \
   lib/panicDefault/Makefile           \
   lib/printer/Makefile                \
   lib/procMgr/Makefile                \
   lib/raster/Makefile                 \
   lib/region/Makefile                 \
   lib/resolution/Makefile             \
   lib/rpcIn/Makefile                  \
   lib/rpcOut/Makefile                 \
   lib/rpcVmx/Makefile                 \
   lib/SLPv2Parser/Makefile            \
   lib/socketMgr/Makefile              \
   lib/string/Makefile                 \
   lib/stubs/Makefile                  \
   lib/sync/Makefile                   \
   lib/syncDriver/Makefile             \
   lib/system/Makefile                 \
   lib/toolsLogger/Makefile            \
   lib/unicode/Makefile                \
   lib/unity/Makefile                  \
   lib/unityStub/Makefile              \
   lib/unityWindowTracker/Makefile     \
   lib/user/Makefile                   \
   lib/vixTools/Makefile               \
   lib/vmBackupLib/Makefile            \
   lib/vmCheck/Makefile                \
   lib/vmSignal/Makefile               \
   lib/wiper/Makefile                  \
   guestd/Makefile                     \
   vmware-user/Makefile                \
   vmware-user-suid-wrapper/Makefile   \
   toolbox/Makefile                    \
   hgfsclient/Makefile                 \
   hgfsmounter/Makefile                \
   checkvm/Makefile                    \
   libguestlib/Makefile                \
   libvmtools/Makefile                 \
   xferlogs/Makefile                   \
   modules/Makefile                    \
   vmblock-fuse/Makefile               \
   scripts/Makefile		       \
])

###
### Output
###
AC_OUTPUT
